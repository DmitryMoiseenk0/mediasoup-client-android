apply plugin: 'com.android.library'
apply plugin: 'maven-publish'

// Source of webrtc.
def WEBRTC_INC_PATH = "${projectDir}/deps/webrtc/src"
// Precompiled static libs and jar for webrtc android.
def WEBRTC_LIB_PATH = "${projectDir}/deps/webrtc/lib"

android {
    compileSdkVersion 30
    ndkVersion "20.0.5594570"

    defaultConfig {
        minSdkVersion 23
        targetSdkVersion 30
        versionCode 309
        versionName "3.1.5"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        externalNativeBuild {
            cmake {
                // Passes optional arguments to CMake.
                arguments "-DLIBWEBRTC_INCLUDE_PATH=" + WEBRTC_INC_PATH,
                        "-DLIBWEBRTC_BINARY_PATH=" + WEBRTC_LIB_PATH,
                        "-DMEDIASOUPCLIENT_BUILD_TESTS=false",
                        "-DSDPTRANSFORM_BUILD_TESTS=OFF",
                        "-DSDPTRANSFORM_BUILD_READMEHELPER=OFF",
                        "-DMEDIASOUPCLIENT_LOG_TRACE=ON",
                        "-DMEDIASOUPCLIENT_LOG_DEV=ON"
            }
        }

        ndk {
            // Specifies the ABI configurations of your native
            // libraries Gradle should build and package with your APK.
            abiFilters 'x86_64', 'armeabi-v7a', 'arm64-v8a'
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    externalNativeBuild {
        cmake {
            // Provides a relative path to your CMake build script.
            path "CMakeLists.txt"
        }
    }
}

repositories {
    flatDir {
        dirs 'deps/webrtc/lib'
    }
}

dependencies {
    compileOnly fileTree(dir: 'deps/webrtc/lib', include: ['libwebrtc.aar'])

    implementation 'androidx.appcompat:appcompat:1.3.1'
    testImplementation 'junit:junit:4.13'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.srcDirs
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.release

                groupId = 'com.sequenia.gitlab.outsource'
                artifactId = 'mediasoup-client'
                version = '3.1.5'
            }
        }
    }
}